  create view forPeriodSelection
  var
  cPer : comp; //  период
  as select *
       from fpPeriod ,
            fpPeriod monthBefore,
            fpPeriod monthAfter
  where ((

  forPeriodSelection.cPer == fpPeriod.nRec and
  fpPeriod.CPREV == monthBefore.nRec and
  fpPeriod.nRec == monthAfter.CPREV
  ))
  ;

  procedure buildUsedPeriods;
  begin
    //message ('doHandlecmTest');
    // f641_selectedperiods

    // отбираем текущий месяц

    delete all f641_selectedperiods;

    var cPrd : comp;
    cPrd := cPeriod;

    insert f641_selectedperiods set
           f641_selectedperiods.rangeid := 1,
           f641_selectedperiods.cPeriod := cPrd
           ;

    VAR curMonth, curKvart, curYear : word;

    set forPeriodSelection.cPer := cPrd;

    if (forPeriodSelection.getFirst fpPeriod = tsOK) {
      curMonth := month(forPeriodSelection.fpPeriod.DBEG);
      curKvart := ( curMonth - 1 ) div 3;
      curYear  := year(forPeriodSelection.fpPeriod.DBEG);
      insert f641_selectedperiods set
             f641_selectedperiods.rangeid := 2,
             f641_selectedperiods.cPeriod := cPrd
             ;
      insert f641_selectedperiods set
             f641_selectedperiods.rangeid := 3,
             f641_selectedperiods.cPeriod := cPrd
             ;
      while (true) {
        if (forPeriodSelection.getFirst fpPeriod <> tsOK) {break};
        if (forPeriodSelection.getFirst monthBefore <> tsOK) {break};
        if ( year(forPeriodSelection.monthBefore.DBEG)  = curYear ){
          //
          insert f641_selectedperiods set
                 f641_selectedperiods.rangeid := 3,
                 f641_selectedperiods.cPeriod := forPeriodSelection.monthBefore.nRec
                 ;
          if (((month(forPeriodSelection.monthBefore.DBEG) -1 ) div 3) = curKvart) {
            insert f641_selectedperiods set
                   f641_selectedperiods.rangeid := 2,
                   f641_selectedperiods.cPeriod := forPeriodSelection.monthBefore.nRec
                   ;
          }
          set forPeriodSelection.cPer := forPeriodSelection.monthBefore.nRec;
        } else {
          break;
        }
      }
      set forPeriodSelection.cPer := cPrd;

      while (true) {
        //break; // периоды "после" отчетного периода - не используем.
        if (forPeriodSelection.getFirst fpPeriod <> tsOK) {break};
        if (forPeriodSelection.getFirst monthAfter <> tsOK) {break};
        if ( year(forPeriodSelection.monthAfter.DBEG)  = curYear ){
          //
          insert f641_selectedperiods set
                 f641_selectedperiods.rangeid := 3,
                 f641_selectedperiods.cPeriod := forPeriodSelection.monthAfter.nRec
                 ;
          if (((month(forPeriodSelection.monthAfter.DBEG) -1 ) div 3) = curKvart) {
            insert f641_selectedperiods set
                   f641_selectedperiods.rangeid := 2,
                   f641_selectedperiods.cPeriod := forPeriodSelection.monthAfter.nRec
                   ;
          }
          set forPeriodSelection.cPer := forPeriodSelection.monthAfter.nRec;
        } else {
          break;
        }
      }
    }
  end;
